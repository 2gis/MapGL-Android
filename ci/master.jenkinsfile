pipeline {

    agent {
        label 'ubuntu-18.04 && x64 && hw'
    }

    environment {
        GRADLE_OPTS = '-Dorg.gradle.daemon=false'
        ARTIFACTORY_HOST='http://maven.v4core.os-n3.hw:8081/artifactory'
    }

    stages {
        stage("prepare") {
            steps {
                script {
                    def localProperties = """\
                        sdk.dir=/opt/android-sdk/

                        apiKey=${params.API_KEY}
                        directionsApiKey=${params.DIRECTIONS_API_KEY}
                    """.stripIndent()

                    writeFile file: "local.properties", text: localProperties
                }

                script {
                    def name = env.JOB_NAME.toLowerCase()

                    def buildInfo = """\
                        buildInfo.build.name=$name
                        buildInfo.build.number=${env.BUILD_ID}
                    """.stripIndent()

                    sh (
                        label: "append build identifiers",
                        script: "echo \"$buildInfo\" >> gradle.properties"
                    )
                }
            }
        }

        stage("build") {
            steps {
                withCredentials([
                    file(credentialsId: 'RELEASE_KEYSTORE', variable: 'RELEASE_KEYSTORE'),
                    string(credentialsId: 'RELEASE_KEYSTORE_PASSWORD', variable: 'RELEASE_KEYSTORE_PASSWORD'),
                    string(credentialsId: 'WEBGL_RELEASE_KEY_ALIAS', variable: 'RELEASE_KEY_ALIAS'),
                    string(credentialsId: 'WEBGL_RELEASE_KEY_PASSWORD', variable: 'RELEASE_KEY_PASSWORD'),
                ]) {
                    sh './gradlew clean build --info'
                    sh './gradlew bundle'

                    // Signing output bundle
                    sh """
                        jarsigner \
                            -keystore \$RELEASE_KEYSTORE \
                            -storepass \$RELEASE_KEYSTORE_PASSWORD \
                            -keypass \$RELEASE_KEY_PASSWORD \
                            build/app/outputs/bundle/release/app-release.aab \
                            \$RELEASE_KEY_ALIAS
                    """
                    sh 'jarsigner -verify build/app/outputs/bundle/release/app-release.aab -keystore \$RELEASE_KEYSTORE'

                    // Signing output apk
                    sh """
                        jarsigner \
                            -keystore \$RELEASE_KEYSTORE \
                            -storepass \$RELEASE_KEYSTORE_PASSWORD \
                            -keypass \$RELEASE_KEY_PASSWORD \
                            -signedjar build/app/outputs/apk/release/app-release.apk \
                            build/app/outputs/apk/release/app-release-unsigned.apk \
                            \$RELEASE_KEY_ALIAS
                    """
                    sh 'jarsigner -verify build/app/outputs/apk/release/app-release.apk -keystore \$RELEASE_KEYSTORE'
                }
            }
        }

        stage("Instrumented tests") {
            steps {
                sh(
                    label: "Подготовка образа эмулятора",
                    script: """
                        echo no | \
                            /opt/android-sdk/tools/bin/avdmanager \
                            create avd \
                            -n webgl-tests \
                            -k "system-images;android-29;google_apis;x86_64" \
                            -f
                    """
                )

                timeout(2) {
                    sh(
                        label: "Поднятие эмулятора",
                        script: """
                            /opt/android-sdk/emulator/emulator \
                                -memory 2048 \
                                -partition-size 2048 \
                                -no-boot-anim \
                                -noaudio \
                                -no-snapshot \
                                -no-window \
                                -avd webgl-tests > /dev/null 2>&1 &
                        """
                    )

                    // Скрипт для проверки инициализации эмулятора, т.к. запускаем его не в
                    // интерактивном режиме.
                    sh(
                        label: "Ожидание загрузки эмулятора",
                        script: """
                            while [ -z "\$(/opt/android-sdk/platform-tools/adb devices | awk '{print \$2}' | grep device)" ];
                            do
                                echo "Waiting for emulator to boot"
                                sleep 5
                            done
                            echo "Emulator booted"
                        """
                    )
                }
                script {
                    try {
                        timeout(10) {
                            sh(
                                label: "Тесты",
                                script: "./gradlew connectedAndroidTest --info"
                            )
                        }
                    }
                    catch (e) {
                        throw e
                    }
                    finally {
                        publishHTML(target: [
                            reportName: 'Sdk tests results',
                            alwaysLinkToLastBuild: true,
                            reportDir: 'build/mapsdk/reports/androidTests/connected',
                            reportFiles: 'index.html',
                            keepAll: true,
                            allowMissing: false,
                        ])

                        publishHTML(target: [
                            reportName: 'App tests results',
                            alwaysLinkToLastBuild: true,
                            reportDir: 'build/app/reports/androidTests/connected',
                            reportFiles: 'index.html',
                            keepAll: true,
                            allowMissing: false,
                        ])

                        timeout(1) {
                            sh(
                                label: 'Saving logcat',
                                script: """
                                    /opt/android-sdk/platform-tools/adb logcat -d > logcat.txt
                                """
                            )
                        }

                        sh(
                            label: "Очистка системы",
                            script: """
                                ps aux | grep emulator | grep -v 'grep' | awk '{print \$2}' | xargs kill

                                /opt/android-sdk/platform-tools/adb kill-server
                            """
                        )

                        archiveArtifacts(artifacts: 'logcat.txt', allowEmptyArchive: true)
                    }
                }
            }
        }

        stage("deploy") {
            steps {
                withCredentials([
                    usernamePassword(
                        credentialsId: 'buildserver-v4core',
                        usernameVariable: 'ARTIFACTORY_USERNAME',
                        passwordVariable: 'ARTIFACTORY_PASSWORD')
                ]) {

                    sh './gradlew mapsdk:artifactoryPublish --info'
                }

                archiveArtifacts(artifacts: 'build/app/outputs/apk/debug/app-debug.apk')
                archiveArtifacts(artifacts: 'build/app/outputs/apk/release/app-release.apk')
                archiveArtifacts(artifacts: 'build/app/outputs/bundle/release/app-release.aab')
            }
        }

        stage("documentation") {
            steps {
                sh './gradlew mapsdk:dokka --info'

                script {
                    ["en", "ru"].each({
                        sh(
                            label: "Deploying $it documentation",
                            script:"""
                                docker run --rm \
                                    -v \$(pwd)/build/mapsdk/dokka:/opt/docsJson \
                                    docker-hub.2gis.ru/2gis-nativesdk/dokka-converter:2.0 \
                                    ./converter.py -u -b /$it/android/webgl/maps/reference/
                            """
                        )
                    })
                }

                withCredentials([
                    string(credentialsId: 'NSDK_GITLAB_PROJECT_TOKEN', variable: 'GITLAB_PROJECT_TOKEN'),
                    string(credentialsId: 'NSDK_GITLAB_PROJECT_ID', variable: 'GITLAB_PROJECT_ID'),
                    string(credentialsId: 'GITLAB_URL', variable: 'GITLAB_URL')
                ]) {
                    sh(
                        label: 'Запуск регенерации документации',
                        script:"""
                            curl --location \
                            -X POST \
                            -F token=$GITLAB_PROJECT_TOKEN \
                            -F ref=master \
                            -F "variables[Android_SDK_CI]=true" \
                            https://${env.GITLAB_URL}/api/v4/projects/${env.GITLAB_PROJECT_ID}/trigger/pipeline
                        """
                    )
                }
            }
        }
    }
}
