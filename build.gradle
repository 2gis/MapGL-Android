// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.72'
    ext.info_extractor_version = "4.16.0"

    repositories {
        google()
        jcenter()
        
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:$info_extractor_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "http://artifactory.2gis.dev/sdk-maven-snapshot" }
    }

    ext {
        isReleaseBuild = project.hasProperty("CI_RELEASE") &&
                Boolean.parseBoolean(project.property("CI_RELEASE"))
    }

    apply plugin: 'com.jfrog.artifactory'
}

subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}

artifactory {
    contextUrl = System.getenv("ARTIFACTORY_HOST") ?: ""
    publish {
        repository {
            repoKey = isReleaseBuild
                    ? "sdk-maven-release"
                    : "sdk-maven-snapshot"
            username = System.getenv("ARTIFACTORY_USERNAME")
            password = System.getenv("ARTIFACTORY_PASSWORD")
        }
        defaults {
            publishBuildInfo = true
            publishArtifacts = true
            publishPom = true
        }
    }
}

artifactoryPublish.skip = true

task clean(type: Delete) {
    delete rootProject.buildDir
}

